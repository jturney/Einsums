#
# ----------------------------------------------------------------------------------------------
#  Copyright (c) The Einsums Developers. All rights reserved.
#  Licensed under the MIT License. See LICENSE.txt in the project root for license information.
# ----------------------------------------------------------------------------------------------
#

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "@EINSUMS_CMAKE_MODULE_PATH@")

include(GNUInstallDirs)
include(einsums_utils)

function(einsums_check_compiler_compatibility)
  if(EINSUMS_IGNORE_COMPILER_COMPATIBILITY)
    return()
  endif()

  if(EINSUMS_WITH_BUILD_BINARY_PACKAGE)
    string(REGEX MATCH "[0-9]+" EINSUMS_MAJOR_VERSION "${EINSUMS_CXX_COMPILER_VERSION}")
    string(REGEX MATCH "[0-9]+" CXX_MAJOR_VERSION "${CMAKE_CXX_COMPILER_VERSION}")

    if(NOT (${CMAKE_CXX_COMPILER_ID} STREQUAL ${EINSUMS_CXX_COMPILER_ID} AND
      ${EINSUMS_MAJOR_VERSION} STREQUAL ${CXX_MAJOR_VERSION}))
      set(MESSAGE "Compilers do not match. In order to compile einsums application it is"
          " recommended to use the same major version of the compiler as you did for einsums. "
          "EINSUMS_CXX_COMPILER=${EINSUMS_CXX_COMPILER}, "
          "CMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}. "
          "EINSUMS_CXX_COMPILER_ID=${EINSUMS_CXX_COMPILER_ID}, "
          "CMAKE_CXX_COMPILER_ID=${CMAKE_CXX_COMPILER_ID}. "
          "EINSUMS_CXX_MAJOR_VERSION=${EINSUMS_MAJOR_VERSION}, "
          "CMAKE_CXX_COMPILER_VERSION=${CXX_MAJOR_VERSION}."
          "To disable this message set EINSUMS_IGNORE_COMPILER_COMPATIBILITY to On.")

      if(EINSUMS_FIND_REQUIRED)
        einsums_error("${MESSAGE}")
      else()
        einsums_warn("${MESSAGE}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "${MESSAGE}" PARENT_SCOPE)
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND False PARENT_SCOPE)
      endif()
    endif()
  else()
    if(NOT (${CMAKE_CXX_COMPILER_ID} STREQUAL ${EINSUMS_CXX_COMPILER_ID} AND
      ${CMAKE_CXX_COMPILER_VERSION} STREQUAL ${EINSUMS_CXX_COMPILER_VERSION}))
      set(MESSAGE "Compilers do not match. In order to compile einsums application it is"
          " recommended to use the same compiler as you did for einsums. "
          "EINSUMS_CXX_COMPILER=${EINSUMS_CXX_COMPILER}, "
          "CMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}. "
          "EINSUMS_CXX_COMPILER_ID=${EINSUMS_CXX_COMPILER_ID}, "
          "CMAKE_CXX_COMPILER_ID=${CMAKE_CXX_COMPILER_ID}. "
          "EINSUMS_CXX_COMPILER_VERSION=${EINSUMS_CXX_COMPILER_VERSION}, "
          "CMAKE_CXX_COMPILER_VERSION=${CMAKE_CXX_COMPILER_VERSION}."
          "To disable this message set EINSUMS_IGNORE_COMPILER_COMPATIBILITY to On.")

      if(EINSUMS_FIND_REQUIRED)
        einsums_error("${MESSAGE}")
      else()
        einsums_warn("${MESSAGE}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "${MESSAGE}" PARENT_SCOPE)
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND False PARENT_SCOPE)
      endif()
    endif()
  endif()
endfunction()

function(einsums_check_boost_compatibility)
  if(EINSUMS_IGNORE_BOOST_COMPATIBILITY)
    return()
  endif()
  if(NOT DEFINED BOOST_ROOT)
    return()
  endif()

  # make sure paths are tested even if not string identical
  get_filename_component(PATH1 "${EINSUMS_BOOST_ROOT}" ABSOLUTE)
  get_filename_component(PATH2 "${BOOST_ROOT}" ABSOLUTE)

  if(NOT PATH1 STREQUAL PATH2)
    einsums_error("The specified BOOST_ROOT differs from what has been used when"
              " configuring and building einsums. Please use the same Boost "
              "versions. einsums Boost is ${EINSUMS_BOOST_ROOT} and user's is ${BOOST_ROOT}. "
              "To disable this message set EINSUMS_IGNORE_BOOST_COMPATIBILITY On."
    )
  endif()
endfunction()

function(einsums_check_allocator_compatibility)
  string(TOUPPER "${EINSUMS_WITH_MALLOC}" EINSUMS_MALLOC_UPPER)
  string(TOUPPER "${EINSUMS_WITH_MALLOC_DEFAULT}" EINSUMS_MALLOC_DEFAULT_UPPER)
  if(NOT (EINSUMS_MALLOC_UPPER STREQUAL EINSUMS_MALLOC_DEFAULT_UPPER))
    einsums_error("EINSUMS_WITH_MALLOC has been changed by this project. This project "
      "has set EINSUMS_WITH_MALLOC='${EINSUMS_WITH_MALLOC}' and einsums was configured "
      "with '${EINSUMS_WITH_MALLOC_DEFAULT}'. EINSUMS_WITH_MALLOC is only provided for "
      "informational purposes to dependent projects and should not be changed.")
  endif()
endfunction()
